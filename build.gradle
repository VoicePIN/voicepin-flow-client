import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'maven'
    id 'pl.allegro.tech.build.axion-release' version '1.4.0'
    id "nebula.project" version "3.3.0"
}

group = 'com.voicepin.flow'
version = scmVersion.version

apply plugin: 'nebula.integtest-standalone'
apply plugin: "com.jfrog.artifactory"

jar {
    manifest {
        attributes("Specification-Title": "VoicePIN Flow REST Client",
                "Implementation-Version": version,
                "Implementation-Revision": getCommitHash())
    }
}

ext {
    jerseyVersion = '2.19'
    log4jVersion = '2.5'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.sun.xml.ws', name: 'jaxws-rt', version: '2.2.5'

    // jersey
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: jerseyVersion
    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: jerseyVersion
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: jerseyVersion
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: jerseyVersion
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseyVersion
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.4.1'

    // logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.14'

    // tests
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.1'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
}

task commitHash << {
    print getCommitHash()
}

def getCommitHash() {
    def repo = Grgit.open(project.file('.'))
    return repo.log(maxCommits: 1).first().id
}

artifactory {
    publish {
        contextUrl = 'http://192.168.66.90:8099/artifactory'
        repository {
            repoKey = "libs-release-local"
            username = voicepinArtifactoryUser
            password = voicepinArtifactoryPassword
        }
        defaults {
            publishConfigs('archives')
        }
    }
}